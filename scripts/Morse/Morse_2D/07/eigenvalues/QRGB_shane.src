!=============================================================================80
!                       Metropolis Monte Carlo Code Grid
!==============================================================================!
!       Discussion:
!Quasi-Regular Gaussian Basis
!using optimized grid solve generalized eigenvalue problem
!==============================================================================!
!       Modified:
!   1 May 2019
!       Author:
!   Shane Flynn 
!==============================================================================!
module QRGB_mod
implicit none
!==============================================================================!
!                            Global Variables 
!==============================================================================!
!d              ==> Gaussian dsionality
!==============================================================================!
integer::d,NG
double precision,parameter::D_morse=12.
double precision,parameter::c_x=0.2041241
double precision,parameter::c_y=0.18371169
!==============================================================================!
contains
!==============================================================================!
function Potential(x)
!==============================================================================!
!       Discussion:
!Hard-coded Morse Potential Energy 
!==============================================================================!
implicit none
double precision::x(d),Potential
Potential=D_morse*((exp(-c_x*x(1))-1)**2+(exp(-c_y*x(2))-1)**2)
end function Potential
!==============================================================================!
end module QRGB_mod
program main
use QRGB_mod
!==============================================================================!
!       Discussion:
!==============================================================================!
!d              ==> i-th gaussian dsionality (x^i=x^i_1,x^i_2,..,x^i_d)
!NG             ==> Number of basis functions
!x              ==>(d) all atom coordinates
!x0             ==>(d) store previous coordinate before trial move
!freq           ==> Interval to update mv_cutoff size
!accept         ==> number of accepted trial moves, for acceptance~50%  
!counter        ==> total number of moves, for acceptance~50%
!x2 ith gaussians coordinates for matrix elements
!z integration sequence form matrix elements
!x_ij= i-jth gaussian center
!z=sequence for integration
!t_i,t_f        ==> cpu time to ~ simulation time
!==============================================================================!
implicit none
character(len=50)::grid_in,alpha_in
integer::Nsobol,data_freq,n,i,j,counter,l,m
integer*8::ii,skip
double precision::kinetic,time1,time2
double precision,allocatable,dimension(:)::alpha,eigenvalues,x_ij
double precision,allocatable,dimension(:,:)::x,Smat,z,Vmat,Hmat
!==============================================================================!
!                       LLAPACK dsygv variables                                !
!==============================================================================!
integer::itype,info,lwork
double precision,allocatable,dimension(:)::work
!==============================================================================!
!                           Read Input Data File                               !
!==============================================================================!
call cpu_time(time1)
read(*,*) d
read(*,*) NG
read(*,*) grid_in
read(*,*) alpha_in
read(*,*) Nsobol
read(*,*) data_freq
!==============================================================================!
!                               Allocations
!==============================================================================!
allocate(x(d,NG),x_ij(d),alpha(NG),eigenvalues(NG),Smat(NG,NG),Vmat(NG,NG))
allocate(z(d,Nsobol),Hmat(NG,NG))
write(*,*) 'Test 0; Successfully Read Input File'
!==============================================================================!
!                           Input GridPoints x(d,NG)
!==============================================================================!
open(16,File=grid_in)
do n=1,NG
    read(16,*) x(:,n)
enddo 
close(16)
!==============================================================================!
!                               Read Alphas
!==============================================================================!
open(17,File=alpha_in)
do n=1,NG
    read(17,*) alpha(n)
enddo 
close(17)
!==============================================================================!
!                           Overlap Matrix (S)
!==============================================================================!
do i=1,NG
    do j=i,NG
       Smat(i,j)=(alpha(i)+alpha(j))**(-0.5*d)&
            *exp(-0.5*alpha(i)*alpha(j)/(alpha(i)+alpha(j))*sum((x(:,i)-x(:,j))**2))
       Smat(j,i)=Smat(i,j)
    enddo
enddo
!==============================================================================!
!                   Check to see if S is positive definite
!If this is removed, you need to allocate llapack arrays before Hamiltonian 
!==============================================================================!
lwork=max(1,3*NG-1)
allocate(work(max(1,lwork)))
call dsyev('v','u',NG,Smat,NG,eigenvalues,work,Lwork,info)
write(*,*) 'Info (Initial Overlap Matrix) ==> ', info
open(unit=19,file='overlap_eigenvalues.dat')
do i=1,NG
    write(19,*) eigenvalues(i)
enddo
close(19)
write(*,*) 'Test 4; Overlap Matrix is Positive Definite'
!==============================================================================!
!                   Generate Sequence For Evaluating Potential
!==============================================================================!
skip=Nsobol
do ii=1,Nsobol
    call sobol_stdnormal(d,skip,z(:,ii))
enddo
write(*,*) 'Test 5; Successfully Generated Integration Sequence'
!==============================================================================! 
!                             Evaluate Potential
!==============================================================================!
Vmat=0d0
counter=0
open(unit=21,file='eigenvalues.dat')
do while(counter.lt.Nsobol/data_freq)
   do i=1,NG
      do j=i,NG
         x_ij(:)=(alpha(i)*x(:,i)+alpha(j)*x(:,j))/(alpha(i)+alpha(j))
         do l=1+(counter*data_freq),(counter+1)*data_freq
            Vmat(i,j)=Vmat(i,j)+Potential(x_ij(:)+z(:,l)/sqrt(alpha(i)+alpha(j))) 
         enddo
      enddo
   enddo
!==============================================================================!
!                   Solve Generalized Eigenvalue Problem
!==============================================================================!
   write(*,*) 'Generalized Eigenvalue Iteration', (counter+1)*data_freq
   Hmat=Vmat
   do m=1,NG
      do n=m,NG
         Smat(m,n)=(alpha(m)+alpha(n))**(-0.5*d)&
              *exp(-0.5*alpha(m)*alpha(n)/(alpha(m)+alpha(n))*sum((x(:,m)-x(:,n))**2))
         Smat(n,m)=Smat(m,n)
         kinetic=Smat(m,n)*0.5*alpha(m)*alpha(n)/(alpha(m)+alpha(n))&
              *sum(1.-alpha(m)*alpha(n)*((x(:,m)-x(:,n))**2)/(alpha(m)+alpha(n)))
         Hmat(m,n)=Hmat(m,n)*Smat(m,n)/((counter+1)*data_freq)+Kinetic
         Hmat(n,m)=Hmat(m,n)
      enddo
   enddo
   itype=1
   eigenvalues=0d0
   call dsygv(itype,'n','u',NG,Hmat,NG,Smat,NG,eigenvalues,work,Lwork,info)
   write(21,*) eigenvalues(:)
   write(*,*) 'info ==> ', info
   counter=counter + 1
enddo
close(21)
!==============================================================================!
!                               output file                                    !
!==============================================================================!
call cpu_time(time2)
open(90,file='simulation.dat')
write(90,*) 'particle dsionality ==> ', d
write(90,*) 'NG ==> ', NG
write(90,*) 'Nsobol==>', Nsobol
write(90,*) 'Total Time ==> ', time2-time1
close(90)
write(*,*) 'Hello Universe!'
end program main
